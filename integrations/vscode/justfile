set windows-shell := ["powershell.exe", "-c"]

default: compile lint

# The primary set of tasks for continuous integration validation.
ci:
  just compile
  just lint
  just test-grammar
  just test

# Setup required dependencies. Use this to bootstrap a new host.
setup:
  npm install

# Compile the application code. Because this is type script, this effectively validates the code.
compile:
  npm run compile

# Run integration tests.
test:
  just _test-{{os()}}

_test-windows:
  npm run test:functional

_test-macos:
  npm run test:functional

# Integration tests require Xvfb enabled environment for Linux, so use that to
# run tests.
_test-linux:
  xvfb-run -a npm run test:functional

# Check the code for compliance with style (lint) rules.
lint:
  npm run lint

# Run grammar snapshot tests to verify syntax highlighting.
test-grammar:
  npm run test:grammar

# Update grammar snapshot files (run after intentional grammar changes).
update-grammar-snapshots:
  npm run test:grammar:update

# Build the VSIX package for this component.
package filename:
  npx vsce package --out {{filename}} --pre-release

# Sets the version number for this component.
version version:
  npm version {{version}}

# Updates dependencies to latest version (or at least tries to)
update:
  npm update

# Publishes a VSIX archive to the Visual Studio Marketplace
publish vsixfilename $VSCE_PAT:
  # vsixfilename: The name of the VSIX archive to publish. 
  #
  # VSCE_PAT: The personal access token for publishing
  ls
  npx vsce publish -p $VSCE_PAT --packagePath {{vsixfilename}} --pre-release 

# Remove build artifacts
clean:
  rm -rf out
  rm -f *.vsix
