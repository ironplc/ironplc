set windows-shell := ["powershell.exe", "-c"]

alias ci := default
default: compile test lint

compile:
  cargo build

test:
  cargo test --all-targets
  cargo llvm-cov --all-features --workspace --fail-under-lines 85 --show-missing-lines

lint:
  cargo clippy
  cargo fmt --all -- --check

# Creates the installer
package version filename:
  just _package-{{os()}} {{version}} {{filename}}

_package-windows version filename:
  cargo build --release
  & "{{env_var('WIX')}}bin\candle.exe" -arch x64 -ext WixUtilExtension -dTargetVendor="pc" -dPlatform=x64 -dVersion="{{version}}" -dCargoTargetBinDir=target\release -o target\wix\main.wixobj wix\main.wxs
  & "{{env_var('WIX')}}bin\light.exe" -spdb -ext WixUIExtension -ext WixUtilExtension -cultures:en-US -out {{filename}} target\wix\main.wixobj

_package-macos version filename:
  echo "Not yet"

_package-linux version filename:
  echo "Not yet"
