set windows-shell := ["powershell.exe", "-c"]

alias ci := default
default: compile test lint

setup:
  cargo install llvm-cov
  cargo install cargo-release

compile:
  cargo build

test:
  cargo test --all-targets
  cargo llvm-cov --all-features --workspace --fail-under-lines 85 --show-missing-lines

lint:
  cargo clippy
  cargo fmt --all -- --check

# Set the version number for this component.
version version:
  cargo release version {{version}} --execute --no-confirm

# Creates the installer or package (as appropriate for platform)
package version filename:
  just _package-{{os_()}} {{version}} {{filename}}

_package-windows version filename:
  cargo build --release
  & "{{env_var('WIX')}}bin\candle.exe" -arch x64 -ext WixUtilExtension -dTargetVendor="pc" -dPlatform=x64 -dVersion="{{version}}" -dCargoTargetBinDir=target\release -o target\wix\main.wixobj wix\main.wxs
  & "{{env_var('WIX')}}bin\light.exe" -spdb -ext WixUIExtension -ext WixUtilExtension -cultures:en-US -out {{filename}} target\wix\main.wixobj
  # Some versions of Windows do something funky with the path so that
  # Get-FileHash is not available. The prefix makes sure we run with a
  # compatible PowerShell instance.
  powershell -noprofile -command '$env:PSModulePath = \"$PSHOME/Modules\"; Get-FileHash -Path "{{filename}}" -Algorithm SHA512 | Select-Object -ExpandProperty Hash | Out-File {{filename}}.sha256'

_package-macos version filename:
  cargo build --release
  tar -czvf {{filename}} target/release/ironplcc
  shasum -a 256 {{filename}} > {{filename}}.sha256

_package-linux version filename:
  cargo build --release
  tar -czvf {{filename}} target/release/ironplcc
  sha256sum {{filename}} > {{filename}}.sha256
